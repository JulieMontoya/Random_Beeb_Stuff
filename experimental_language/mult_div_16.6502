\  NOW INCLUDES  (WE THINK)  ALL THE DYADIC OPERATIONS!
\  STILL WORKS WITH MINIMAL MUCKING ABOUT
\  NB! SOME ENTRY ADDRESSES HAVE CHANGED!

\  TRYING TO BE SPARING WITH ZERO PAGE

\  PRUNING OUT UNNECESSARY STACKY STUFF

product=&70
multiplicand=&74
multiplier=&70
preload=&72
dividend=&70
divisor=&74
quotient=&70
remainder=&72

W_reg = &70
X_reg = &72
O_reg = &74

calc_stk = &7000
calc_sp = &7E
scratch_X = &7F
alt_sp = &7F
neg_flag = &7F

upper_ofs = &7E
lower_ofs = &7F

var_ptr   = &76
cal_stk   = &7A

\  POINTERS ARE IN &80 - &8F

code_ptr = &80
str_ptr  = &82
params   = &84          \  these get stomped on by INPUT!
par_loc  = &85
returns  = &86
for_ptr  = &88
pib      = &8A

num_dims = &86
opcode   = &87
for_sp   = &8E

rnd_seed = &0D          \  BBC BASIC uses this as RND seed

osasci = &FFE3
oswrch = &FFEE
osword = &FFF1

\  SOME MORE CONSTANTS

CR       = &0D

instr_addr = &70FE

CLEAR calc_stk, calc_stk + &FD
ORG calc_stk + &FD

.do_operation
    JMP _safe_return

.init_calc_stk
    JMP real_init_calc_stk
\.ext_disp_dec_stk
\    JMP disp_dec_W
.ext_push16
    JMP newpush16
.ext_get_instr
    JMP get_instr

\.ext_zx_rnd
\    JMP zx_rnd

.ext_get_array_base
    JMP get_array_base
.ext_rev_stack
    JMP rev_stack
.twc16
    JMP real_twc16
.parse_num_stk
    JMP real_parse_num_stk
.dup
    JMP real_dup
.ext_signed_divide
    JMP signed_divide
.conv_str_stk
    JMP real_conv_str_stk

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  INITIALISE CALCULATION STACK

.real_init_calc_stk
    LDA #(calc_stk + &FC) MOD 256
    STA cal_stk
    LDA #(calc_stk + &FC) DIV 256
    STA cal_stk+1
._safe_return
    RTS
    

\  PUSH A 16-BIT VALUE FROM ZERO PAGE ONTO THE CALCULATION STACK
\  ON ENTRY, X => LOCATION OF DATA IN ZP

.newpush16
    LDA 0,X
    JSR push_byte
    LDA 1,X
.push_byte
    LDY cal_stk
    BNE _phb1
    DEC cal_stk+1
._phb1
    DEC cal_stk
    LDY #0
    STA (cal_stk), Y
    RTS
    

\  PULL A 16-BIT VALUE FROM THE CALCULATION STACK AND STORE IN ZERO PAGE
\  ON ENTRY, X => LOCATION IN ZP FOR DATA

.pull16
    LDY #0
    JSR pull_byte
    STA 1, X
    JSR pull_byte
    STA 0,X
    RTS

.pull_byte
    LDA (cal_stk), Y
    INC cal_stk
    BNE _plb1
    INC cal_stk+1
._plb1
    RTS
    
\  DUP : DUPLICATE THE VALUE AT THE TOP OF THE CALCULATION STACK
\

.real_dup
    LDY calc_sp
    JSR _dup2
    STY calc_sp
    RTS
._dup2
    JSR _dup1
._dup1
    DEY
    LDA calc_stk+2,Y
    STA calc_stk,Y
    RTS
    
\  GET LEFT AND RIGHT OPERANDS FROM STACK

.get_both
.pull_OW
    JSR get_rho
.get_lho
.pull_W
    LDX #W_reg
    JMP pull16
.get_rho
.pull_O
    LDX #O_reg
    JMP pull16

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\

\.vdu_stk
\    JSR get_rho
\    LDA O_reg
\    JMP oswrch
    
\.vdw_stk
\    JSR get_rho
\    LDA O_reg
\    JSR oswrch
\    LDA O_reg+1
\    JMP oswrch

\  DISPLAY A DECIMAL NUMBER FROM TOP OF STACK

.disp_dec_W
    LDA W_reg+1
    STA neg_flag
    BPL _ddW_pos
    JSR twc16_W
._ddW_pos
    JSR decode_dec_W
._ddW3
    LDA neg_flag
    BPL _ddW4
    LDA #45
    STA calc_stk,X
    INX
._ddW4
    LDA calc_stk-1,X
    JSR osasci
    DEX
    BNE _ddW4
    RTS
    
\  STORE STRING REPRESENTATION OF A NUMBER IN MEMORY

.real_conv_str_stk
    LDX #str_ptr        \  Points to destination to store string
    JSR copy_W_out      \  W holds destination
    JSR pull16       \  Value is beneath it
    JSR decode_dec_stk
    LDY #0
    LDA neg_flag
    BPL _cvs2
    LDA #45
    STA (str_ptr),Y
    INY
._cvs2
    LDA calc_stk-1,X
    STA (str_ptr),Y
    INY
    DEX
    BNE _cvs2
    LDA #13             \  final character is CR
    STA (str_ptr),Y
    RTS
    
    RTS

.decode_dec_stk
    LDY calc_sp
    LDA calc_stk,Y
    STA neg_flag
    BPL _dds_pos
    JSR real_twc16
    LDA #128
._dds_pos
    \  We are going to divide the top of the stack ...
    LDX #dividend
    JSR pull16
    \  ... by 10
    LDA #10
    JSR set_divisor
    TAX                 \  we know A=0 here
._dds1
    JSR real_div16
    \  Store the digit's ASCII code in scratch space
    LDA remainder
    CLC
    ADC #48
    CMP #58             \  see if we need to correct a hex digit
    BCC _dds2
    ADC #6              \  here, C=1 so we add one fewer than we need
._dds2
    STA calc_stk,X
    INX
    \  See if working total has reached zero
    LDA dividend
    ORA dividend+1
    BNE _dds1
._dds_end
    RTS

\  PARSE A NUMBER OUT OF A STRING AT SOME LOCATION, GIVEN ON THE STACK;
\  AND PLACE ITS NUMERIC VALUE ON THE STACK.

.real_parse_num_stk
    LDX #str_ptr        \  set up string pointer
    JSR pull16          \  pull value from stack to where X points

\  THE SAME, BUT EXPECTS STR_PTR TO BE ALREADY SET UP

.parse_num_at_ptr
    LDA #10
    JSR set_multiplicand
    STA neg_flag        \  here we know A=0
    TAX
._pn0
    STA product,X
    INX
    CPX #4
    BCC _pn0
    TAY
._pn1
    LDA (str_ptr),Y
    CMP #13             \  see if it's CR
    BEQ _pn_cr
    CMP #32             \  see if it's a space
    BEQ _pn_space
    BCC _pn_notdigit    \  end of number if not a digit
    CMP #45             \  see if it's a minus sign
    BEQ _pn_minus
    CMP #58             \  see if it's ":" or greater
    BCS _pn_notdigit
    CMP #48             \  see if it's smaller than "0"
    BCC _pn_notdigit
    SEC                 \  take away 48
    SBC #48
    STA product+2       \  store it in the preload register
    LDA #0
    STA product+3
    JSR mult16_preload  \  multiply total by 10 and add new digit
._pn_space
    INY
    BNE _pn1            \  not expected to fall through
._pn_notdigit           \  not a digit
._pn_cr
    LDX #product        \  this is the value we parsed
    JSR newpush16
    LDA neg_flag        \  see if we have to negativify it
    BPL _pn_pos
    JMP real_twc16      \  take twos complement of TOS and return

\  Set the negative flag; gets called if we see a minus sign

._pn_minus
    LDA #255
    STA neg_flag
    BNE _pn_space       \  always branches
._pn_pos
    RTS

\  ADVANCE STRING POINTER BY OFFSET IN Y

.adv_str_ptr
    LDX #str_ptr
    
\  ADVANCE A ZERO-PAGE POINTER AT X BY AN OFFSET IN Y
\
\  SUPPOSE WE HAVE BEEN DOING A LOT OF  LDA (zp), Y  INSTRUCTIONS
\  NOW WE WANT TO ADVANCE THE POINTER SO WE CAN BEGIN READING THE
\  NEXT RECORD WITH Y=0.
\
\  TO SAVE THE NEED FOR MULTIPLE COPIES OF ESSENTIALLY THE SAME CODE,
\  WE USE THE 6502 X REGISTER TO SPECIFY THE ADDRESS OF THE POINTER.
\  AS LONG AS WE REMEMBER TO INCREASE Y AFTER THE LAST BYTE IS READ
\  THEN Y WILL BE THE CORRECT AMOUNT TO ADVANCE THE POINTER BY.

.adv_ptr_X
    TYA                 \  This already holds amount to advance
.adv_ptr_XA             \  Extra entry points cost nothing
    CLC
    ADC 0,X
    STA 0,X
    BCC _ap_1           \  Skip if carry is clear
    INC 1,X             \  Increase high byte if we had to carry one
._ap_1
    RTS
    
\  BACK UP A POINTER AT X BY AN AMOUNT IN A
\  NOTE, THE SUBTRAHEND IS ALREADY IN A, SO WE TAKE ITS TWOS COMPLEMENT
\  AND ADD THAT.  (SBC WOULD EXPECT THE MINUEND IN A.)

.backup_ptr_X
    EOR #&FF            \  Twos-complement A. Flip the bits ...
    SEC                 \  ... and prepare to add one.
    ADC 0, X            \  Now we have subtracted A from the low byte
    STA 0, X            \  Store it back
    BCS _bp_1           \  Skip if carry is set  (i.e. nothing to borrow)
    DEC 1, X            \  Decrease the high byteif we had to borrow one
._bp_1
    RTS
    
    

\  STORE A WORD IN MEMORY -- ! OPERATOR / VARIABLE ASSIGNMENT

.write_word_stk
JSR get_rho
._write_word_in_place
\  assume address is in place in O_reg and word to write is at TOS
LDY #2
LDX calc_sp
JSR _wrw2
STX calc_sp
RTS
._wrw2
JSR _wrw1
._wrw1
LDA calc_stk,X
INX
DEY
STA (O_reg),Y
RTS

\  READ A WORD FROM MEMORY -- ! OPERATOR (RHS)

.read_word_stk
    JSR get_rho
    LDY #0
    LDX calc_sp
    JSR _rw2
    STX calc_sp
    RTS
._rw2
    JSR _rw1
._rw1
    LDA (O_reg),Y
    INY
    DEX
    STA calc_stk,X
    RTS

\  READ A STRING FROM THE KEYBOARD -- INPUT $

.input_str
    LDY #0              \  create control block in ZP at str_ptr
    STY str_ptr         \  byte 0 => address low
    LDA #7
    STA str_ptr+1       \  byte 1 => address high
    LDA #255
    STA str_ptr+2       \  byte 2 => maximum length
    STA str_ptr+4       \  byte 4 => maximum ASCII code
    LDA #32
    STA str_ptr+3       \  byte 3 => minimum ASCII code
    TYA
    LDX #str_ptr
    JSR osword          \  OSWORD 0 => INPUT
    RTS                 \  with string at &700

\  READ A NUMBER FROM THE KEYBOARD -- INPUT

.input_num
    JSR input_str
    JMP parse_num_at_ptr
    \  Now we have a numeric value on the stack
    
    
    
.input_cblk
    EQUW &700           \  bytes 0-1 => address
    EQUB 255            \  byte 2 => maximum length
    EQUB 32             \  byte 3 => minimum ASCII code
    EQUB 255            \  byte 4 => maximum ASCII code

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  MONADIC OPERATIONS
\
\  TWOS-COMPLEMENT THE TOP OF THE STACK
\

.real_twc16
    LDY calc_sp
    SEC                 \  so as to add 1 when we flip the bits
    LDA #0
    SBC calc_stk+1,Y
    STA calc_stk+1,Y
    LDA #0
    SBC calc_stk,Y
    STA calc_stk,Y
    RTS

\  TWOS-COMPLEMENT NUMBERS VARIOUSLY IN O, W AND [X]

.twc16_O
    LDX #O_reg
    BNE twc16_zpX       \  always branches
.twc16_W
    LDX #W_reg
.twc16_zpX
    LDY calc_sp
    SEC                 \  so as to add 1 when we flip the bits
    LDA #0
    SBC 0,X
    STA 0,X
    LDA #0
    SBC 1,X
    STA 1,X
    RTS

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\

.mult16
.real_mult16
.oper_MUL
    LDA #0
    STA product+3
    STA product+2
.mult16_preload
    TXA                 \  save X and Y on 6502 stack
    PHA
    TYA
    PHA
    \  Begin by shifting the multiplier right so its lowest bit is already
    \  in the carry flag.
    LDY #17             \  one more than we need
    BNE _mult16_3       \  always branches; A = 0
._mult16_1
    BCC _mult16_2
    \  Add the multiplicand to the high word of the product
    CLC
    LDA product+2
    ADC multiplicand
    STA product+2
    LDA product+3
    ADC multiplicand+1
    STA product+3
    \  Shift the product right
._mult16_2
    ROR product+3
    ROR product+2
._mult16_3
    ROR product+1
    ROR product
    \  See if we need to go around again
    DEY
    BNE _mult16_1
    
    PLA                 \  retrieve Y and X from 6502 stack
    TAY
    PLA
    TAX
    RTS

\  This is the same bit of code with 2 different names

.set_divisor
.set_multiplicand
    STA multiplicand
    LDA #0
    STA multiplicand+1
    RTS
    
\  DIVIDE ONE 16-BIT NUMBER BY ANOTHER
    
.real_div16
.divide_no_rem
    LDA #0
    STA remainder
    STA remainder+1

\  DIVIDE A 32-BIT PRODUCT IN THE W AND X REGISTERS BY A DIVISOR IN THE
\  O REGISTER

.oper_DIP
.divide
    TXA                 \  save X and Y on 6502 stack
    PHA
    TYA
    PHA
    JSR div_core
    PLA                 \  retrieve Y and X from 6502 stack
    TAY
    PLA
    TAX
    RTS

\  SIGNED DIVISION.  THIS WORKS BY KEEPING THE DIVIDEND AND DIVISOR POSITIVE
\  AND USING neg_flag TO INDICATE WHAT IF ANYTHING NEEDS TO BE MADE NEGATIVE
\  AFTER THE OPERATION IS COMPLETE.

.oper_DIV
.signed_divide
    LDA #0
    STA remainder
    STA remainder+1
.signed_div_ext
    TXA                 \  save X and Y on 6502 stack
    PHA
    TYA
    PHA
    LDA #0
    STA neg_flag
    LDA dividend+1
    BPL positivify_divr
    LDA #&C0            \  set bits 7 and 6
    STA neg_flag
    LDX #dividend       \  twos-complement it
    JSR twc16_zpX
.positivify_divr
    LDA divisor+1
    BPL _pos_dvr1
    LDA neg_flag
    EOR #&80
    STA neg_flag
    LDX #divisor        \  twos-complement it
    JSR twc16_zpX
._pos_dvr1
    JSR div_core
    BIT neg_flag
    BPL _sd_quot_pos
    LDX #quotient
    JSR twc16_zpX       \  negativify the quotient
._sd_quot_pos
    BIT neg_flag
    BVC _sd_rem_pos
    LDX #remainder
    JSR twc16_zpX       \  negativify the remainder
._sd_rem_pos
    PLA                 \  retrieve Y and X from 6502 stack
    TAY
    PLA
    TAX
    RTS
    
\  CORE DIVISION ROUTINE.  THIS TRASHES REGISTERS .....

.div_core
    LDY#17              \  one more than we need
    BNE _divide_3       \  do an extra left shift on just bottom bits
._divide_1
    ROL dividend+2
    ROL dividend+3
._divide_2
    SEC
    LDA dividend+2
    SBC divisor
    TAX \ stash low byte in X in case we need it
    LDA dividend+3
    SBC divisor+1
    BCC _divide_3
    \ update dividend if we had room to subtract
    STX dividend+2
    STA dividend+3
._divide_3
    ROL dividend      \ C shifts into divd
    ROL dividend+1
    DEY
    BNE _divide_1
    \  dividend, dividend+1 now contain quotient
    \  dividend+2, dividend+3 contain remainder
    RTS

\\  MULTIPLY TWO NUMBERS AT TOP OF STACK
\    
\.mul_stk
\    JSR get_both
\.mul_in_place
\    JSR real_mult16
\.return_stk
\    LDX #product
\    JMP push16
\
\\  DIVIDE NUMBER JUST BELOW TOP OF CALCULATION STACK BY NUMBER ON TOP
\\  RETURN QUOTIENT ON TOP OF CALCULATION STACK
\    
.div_stk
RTS
\    JSR get_both
\.div_in_place
\    JSR signed_divide
\    JMP return_stk
\
\\  DIVIDE NUMBER JUST BELOW TOP OF CALCULATION STACK BY NUMBER ON TOP
\\  RETURN QUOTIENT ON TOP OF CALCULATION STACK
\    
\.real_mod_stk
\    JSR get_both
\.mod_in_place
\    CLC
\    BCC adc16_in_place  \  always branches
\    JSR signed_divide
\.return_ext
\    LDX #X_reg          \  return remainder
\    JMP push16

\\  ADD TWO NUMBERS AT TOP OF CALCULATION STACK
\\  RETURN SUM ON TOP OF CALCULATION STACK
\
\.add16_in_place
\    CLC
\    BCC adc16_in_place  \  always branches
\.add16_stk
\    CLC
\.adc16_stk
\    JSR get_both
\    LDX #0
\.adc16_in_place
\    JSR _adc_2
\    JMP return_stk
\._adc_2
\    JSR _adc_1
\._adc_1
\    LDA W_reg,X
\    ADC O_reg,X
\    STA W_reg,X
\    INX
\    RTS
\    
\\  SUBTRACT NUMBER AT TOP OF CALCULATION STACK FROM NUMBER BELOW IT
\\  RETURN DIFFERENCE ON TOP OF CALCULATION STACK
\
\.sub16_in_place
\    SEC
\    BCS sbc16_in_place  \  always branches
\.sub16_stk
\    SEC
\.sbc16_stk
\    JSR get_both
\.sbc16_in_place
\    LDX #0
\    JSR _sbc_2
\    JMP return_stk
\._sbc_2
\    JSR _sbc_1
\._sbc_1
\    LDA W_reg,X
\    SBC O_reg,X
\    STA W_reg,X
\    INX
\    RTS

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  DOUBLE INDIRECTION
\
\  On entry, X points to a ZP location.  The 16-bit value to which this
\  points is read and stored over the address at X in ZP.

.get_dbl_ind
    LDA (0,X)           \  get low byte
    PHA                 \  store it on 6502 stack
    JSR inc16_X         \  advance pointer to high byte
    LDA (0,X)           \  get high byte
    STA 1,X             \  store high byte over high byte of address
    PLA                 \  retrieve low byte from 6502 stack
    STA 0,X             \  store low byte over low byte of address
    RTS
.inc16_X                \  increase 16-bit value at X in ZP
    INC 0,X
    BNE _i16_1
    INC 1,X
._i16_1
    RTS

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  REVERSE STACK

.rev_stack

\  A = number of values on the stack to reverse

    TAX
    DEX
    TXA                 \  now one smaller than we need
    ASL A               \  double it
    STA lower_ofs       \  this points to the lower value
    LDY #0
    STY upper_ofs       \  this points to the upper value
._rv_stk1
    LDY upper_ofs
    CPY lower_ofs
    BCS _rv_stk2        \  Terminate before we cross over
    JSR _rv_swap_byte   \  Now upper_ofs has been increased by 1
    INC lower_ofs       \  Ready for high byte at lower end
    LDY upper_ofs
    JSR _rv_swap_byte
    TYA                 \  this is lower_ofs
    SBC #2              \  C=0; we got here when BCS fell through
    STA lower_ofs
    BNE _rv_stk1        \  it won't get as low as 0
._rv_stk2
    RTS
    
._rv_swap_byte
    LDA (cal_stk), Y
    PHA
    LDY lower_ofs
    LDA (cal_stk), Y
    LDY upper_ofs
    STA (cal_stk), Y
    INY
    STY upper_ofs       \  move it on for the next byte
    PLA
    LDY lower_ofs
    STA (cal_stk), Y
    RTS                 \  without altering lower_ofs

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  CLOSE A GAP IN THE STACK

\  ON ENTRY upper_ofs POINTS TO END OF BLOCK TO BE MOVED
\                                  (MAKE SURE IT'S ODD!)
\           lower_ofs POINTS TO NEW END OF BLOCK
\                                  (ALSO MUST BE ODD)

.close_gap
._cg1
    LDY upper_ofs       \  Points to bottom of block being moved
    LDX upper_ofs       \  Make a copy of it
    LDA (cal_stk), Y
    DEY
    STY upper_ofs
    LDY lower_ofs       \  Get location to move it to
    STA (cal_stk), Y
    DEY
    STY lower_ofs
    TXA                 \  See if we copied from offset 0
    BNE _cg1
._cg_end
    RTS
    
    
    
\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  ROLL STACK
\
\
\  THIS IS ALSO GOING TO NEED SOME REWORKING!

\.roll_stack
\    LDY calc_sp
\    TYA                 \  Y goes up from stack ptr
\    CLC
\    ADC W_reg
\    ADC W_reg           \  might as well just add it twice
\    \  Now A points W values into the stack
\    TAY    
\    \  Stash this value on the 6502 stack
\    LDA calc_stk-2, Y
\    PHA
\    STA &75
\    LDA calc_stk-1, Y
\    PHA
\    STA &74
\    STY &76
\    LDX #0
\._rl_stk0
\    JSR _rl_stk1
\    INX
\    CPX W_reg
\    BCC _rl_stk0
\._rl_stk_end
\    PLA
\    STA calc_stk+1,Y
\    PLA
\    STA calc_stk, Y
\    RTS
\        
\._rl_stk1               \  copy a value down the stack
\    JSR _rl_stk2
\._rl_stk2
\    LDA calc_stk-3, Y
\    STA calc_stk-1,Y
\    DEY
\    RTS

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  ARRAY ACCESS
\
\  ON ENTRY;    ARRAY BASE
\               LAST SUBSCRIPT
\               .....
\               FIRST SUBSCRIPT
\  ON EXIT, var_ptr HOLDS ADDRESS OF SELECTED ELEMENT WITHIN ARRAY.

.get_array_base
    LDX #var_ptr        \  copy base address from W
    JSR copy_W_out
    LDY #0
    LDA (var_ptr), Y    \  Byte 0 = number of dimensions
    STA num_dims
    INY
    LDX #var_ptr
    JSR adv_ptr_X
    LDA num_dims
    CMP #2
    BCC _ab_1dim
    JSR rev_stack       \  Now subscripts are in the right order
._ab_1dim
    LDA #0
    STA str_ptr+2       \  Running total
    STA str_ptr+3
._ab_subscript
    LDY #0
    LDA (var_ptr), Y    \  Get dimension ...
    INY
    STA O_reg           \  ... and place in O
    LDA (var_ptr), Y
    INY
    STA O_reg+1
    LDX #var_ptr
    JSR adv_ptr_X       \  Advance pointer
    LDX #str_ptr+2      \  Copy running total to W
    JSR copy_to_W
    JSR oper_MUL        \  Multiply by group size
    LDX #str_ptr+2      \  Copy back to running total
    JSR copy_W_out      \  Copy back to running total
    JSR pull_W          \  Get subscript from Stack
    JSR oper_MOD        \  Reduce subscript modulo dimension
    BPL _ab_subscr_pos  \  Skip if remainder is positive
    JSR oper_ADD        \  add O to W; now it's positive
._ab_subscr_pos
    LDA W_reg           \  update running total in place
    CLC
    ADC str_ptr+2
    STA str_ptr+2
    LDA W_reg+1
    ADC str_ptr+3
    STA str_ptr+3
    DEC num_dims        \  next subscript
    BNE _ab_subscript
.ab_all_subs
    ASL str_ptr+2       \  running total is counting elements, not bytes ...
    ROL str_ptr+3       \  ... so double it to get bytes ...
    LDA var_ptr         \  ... and add it to pointer
    CLC
    ADC str_ptr+2
    STA var_ptr
    LDA var_ptr+1
    ADC str_ptr+3
    STA var_ptr+1
    RTS

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  DYADIC OPERATIONS

.oper_ADD
    CLC
    LDA W_reg
    ADC O_reg
    STA W_reg
    LDA W_reg+1
    ADC O_reg+1
    STA W_reg+1
    RTS
    
.oper_SUB
    SEC
    LDA W_reg
    SBC O_reg
    STA W_reg
    LDA W_reg+1
    SBC O_reg+1
    STA W_reg+1
    RTS

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  TESTS

.oper_TEQ               \  TEQ      = in BASIC
    LDA W_reg
    EOR O_reg
    BNE ret_false       \  false if high bytes differ
    LDA W_reg+1
    EOR O_reg+1
    BNE ret_false       \  false if low bytes differ
.ret_true
    LDA #&FF
    BNE ret_any
.ret_false
    LDA #&00
.ret_any
    STA W_reg
    STA W_reg+1
    RTS

.oper_TNE               \  TNE      <> in BASIC
    LDA W_reg
    EOR O_reg
    BNE ret_true        \  true if high bytes differ
    LDA W_reg+1
    EOR O_reg+1
    BNE ret_true        \  true if low bytes differ
    BEQ ret_false

.oper_TLT               \  TLT      < in BASIC
    JSR _tlt_tge
    BMI ret_true
    BPL ret_false

.oper_TGE               \  TGE      >= in BASIC
    JSR _tlt_tge
    BMI ret_false
    BPL ret_true

.oper_TGT               \  TGT      > in BASIC
    JSR _tgt_tle
    BMI ret_true
    BPL ret_false

.oper_TLE               \  TLE      <= in BASIC
    JSR _tgt_tle
    BMI ret_false
    BPL ret_true

._tlt_tge
    SEC
    LDA W_reg
    SBC O_reg
    LDA W_reg+1
    SBC O_reg+1
    BVC _t_n_ok         \  V=0 => N is correct
._t_n_ng
    EOR #&80            \  toggle sign bit
._t_n_ok
    RTS
    
._tgt_tle
    SEC
    LDA O_reg
    SBC W_reg
    LDA O_reg+1
    SBC W_reg+1
    BVS _t_n_ng
    RTS

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  MONADIC OPERATIONS

\  SGN
\
\  If a number is negative, its high byte will definitely not be zero.
\  If a number is positive and >255, its high byte also will not be 0.
\  Therefore, if the high byte is not zero, its sign determines the sign
\  of the number.
\  If the high byte is zero, we have to look at the low byte, but need
\  to beware lest 128-255 give "false negatives".
\  We know the number is positive if not 0.
\  If the high byte is zero, the number must be either zero or positive.
\  If zero, we return with Z=1 (and N=0, by the 6502's internal logic).
\  If non-zero, we set A=1 in order to force Z=0 and N=0.

.oper_SGN
    LDA W_reg+1
    BNE _sgn_right      \  Z=0, N=correct sign
    LDA W_reg
    BEQ _sgn_right      \  Z=1, N=0
    LDA #1              \  force Z=0, N=0
._sgn_right
.oper_NCH               \  this returns a number unchanged
    RTS
    
.oper_ABS
    LDA W_reg+1
    BMI _abs_neg
._abs_pos
    RTS
._abs_neg
.oper_TWC
    LDX #W_reg
    JMP twc16_zpX
    
.oper_ONC
    LDA W_reg
    EOR #&FF
    STA W_reg
    LDA W_reg+1
    EOR #&FF
    STA W_reg+1
    RTS
    
\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\

    
\  ENTER A PROCEDURE
\
\  WILL NEED REFACTORING FOR NEW-STYLE STACK

\.proc
\    JSR read_pib_header
\    LDX #0
\.store_param
\    CPX par_loc
\    BCS _sp_done
\    
\    \  Store the variable location and its present value on the Stack.
\    \  We have to do this for all parameters and local variables.
\    
\    LDA (pib), Y
\    STA W_reg
\    INY
\    LDA (pib), Y
\    STA W_reg+1
\    INY
\    TYA
\    PHA
\    LDY #0
\    LDA (W_reg), Y
\    STA O_reg
\    INY
\    LDA (W_reg), Y
\    STA O_reg+1
\    TXA
\    PHA
\    \LDX #W_reg         \  No need for address on Stack
\    \JSR push16
\    LDX #O_reg          \  Just the old value
\    JSR push16
\    PLA
\    TAX
\    CPX params
\    BCS _sp_local
\    
\    \  This is a parameter, so we have to populate its variable (whose
\    \  address is still in W)  with a value from lower down the Stack.
\    
\    TXA
\    PHA
\    LDY #0
\    LDX alt_sp          \  Pointer to parameters
\    JSR _fill_param
\    STX alt_sp
\    PLA
\    TAX
\._sp_local
\    PLA
\    TAY
\    INX
\    BNE store_param     \  Not expecting to fall through
\._sp_done
\    LDX #code_ptr       \  Store return address on Stack
\    JSR push16
\    LDX #pib            \  Store address of PIB on Stack
\    JMP push16
\    
\._fill_param
\    JSR _fp1
\._fp1
\    DEX
\    LDA calc_stk, X
\    STA (W_reg), Y
\    INY
\    RTS
\
\\  READ PARAMETER INFO BLOCK HEADER
\\  byte 0 => parameter count
\\  byte 1 => local variables count
\\  byte 2 => returns count
\\   ... followed by addresses of parameters
\\   ... followed by addresses of local variables
\\   ... followed by code
\
\.read_pib_header
\    LDY #0              \  0 => parameter count
\    LDA (pib), Y
\    STA params          \  parameter count
\    INY                 \  1 => local variable count
\    CLC
\    ADC (pib), Y
\    STA par_loc         \  parameters + locals count
\    INY                 \  2 => returned count
\    LDA (pib), Y
\    STA returns         \  returned count
\    INY
\    RTS
\    
\\  RESTORE LOCAL VARIABLES AND VARIABLES USED FOR PARAMETERS FROM BELOW ANY
\\  RETURNED VALUES ON THE CALCULATION STACK
\
\.endproc
\    \\\\  THIS IS NOT QUITE RIGHT
\    \  We need to get the address of the PIB, which is some way down the
\    \  Stack, *before* we can read its header!
\    JSR read_pib_header
\    LDA par_loc         \  number of values to restore
\    ASL A               \  we know C=0
\    ADC #3              \  clear header
\    TAY
\    LDA calc_sp
\    STA alt_sp          \  make copy of Stack pointer
\    ADC returns         \  Remember this counts values ...
\    ADC returns         \  ... so we need to add twice 
\    ADC #4              \  clear return and PIB addresses
\    STA calc_sp         \  now Stack ptr points to saved values
\.restore_param
\    CPY #5
\    BCC _rp_done
\    DEY
\    LDA (pib), Y
\    STA W_reg+1
\    DEY
\    LDA (pib), Y
\    STA W_reg           \  now W holds address of variable
\    TYA
\    PHA
\    LDX #O_reg
\    JSR pull16          \  pull value from stack into O
\    LDY #0
\    LDA O_reg
\    STA (W_reg), Y      \  store it back
\    INY
\    LDA O_reg+1
\    STA (W_reg), Y
\    PLA
\    TAY
\    BNE restore_param   \  Not expecting to fall through
\._rp_done
\    \  Now we need to move the returned values and return address back
\    \  down the stack, overwriting the original parameters.
\    LDA calc_sp
\    CLC
\    ADC params
\    ADC params
\    STA calc_sp         \  Now calc_sp is below the parameters.
\    TAY
\    LDA alt_sp
\    CLC
\    ADC returns
\    ADC returns
\    ADC #4              \  for the PIB and return addresses
\    TAX                 \  Now X points below the returns.
\    \\\\  TOO LATE! We should have done this earlier
\    \\\\LDX #pib            \  Get address of PIB from Stack
\    \\\\JSR pull16
\._rp_close_gap
\    CPX alt_sp
\    BEQ _rp_cg_done
\    LDA calc_stk-1, X
\    DEX
\    STA calc_stk-1, Y
\    DEY
\    BNE _rp_close_gap   \  Not expecting to fall through
\._rp_cg_done
\    STY calc_sp         \  Now top of stack is above return stuff
\    RTS
\    \  If we have any values to return, we need to roll the return
\    \  address to the top of the Stack.
\    LDX returns
\    BEQ _rp_no_roll
\    INX
\    STX W_reg
\    JSR roll_stack
\._rp_no_roll
\    RTS
    
\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  ZX81/SPECTRUM PSEUDO-RANDOM NUMBER GENERATOR

.zx_rnd
    LDA rnd_seed
    STA W_reg
    LDA rnd_seed+1
    STA W_reg+1
    \  Add 1 to the seed
    INC W_reg
    BNE _zx_rnd1
    INC W_reg+1
._zx_rnd1
    LDA #75             \  a primitive root modulo 65537
    JSR set_multiplicand
    JSR mult16
    \  Reduce product modulo 65537
    LDA X_reg+1
    BEQ _zx_rnd_done    \  If answer < 65536, our work is done
    CMP #2
    BCC _zx_rnd_2       \  If 65536s > 1, definitely need to mung
    LDA W_reg
    ORA W_reg+1
    BEQ _zx_rnd_done    \  If answer is exactly 65536, our work is done
._zx_rnd_2
    LDA W_reg           \  Subtract 65536s from units, 256s
    SEC
    SBC X_reg+1
    STA W_reg
    LDA W_reg+1
    SBC #0
    STA W_reg+1
._zx_rnd_done
    LDA W_reg           \  Subtract 1 from remainder and store as new seed
    SEC
    SBC #1
    STA rnd_seed
    LDA W_reg+1
    SBC #0
    STA rnd_seed+1
    RTS
    
\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\
\  FOR AND NEXT
\
\  STACK BITS WILL NEED LOOKING AT

\.oper_NXT
\    \  Find the last entry on the FOR stack
\    LDA for_sp
\    SEC
\    SBC #8
\    STA alt_sp          \  save ctrl var addr
\    TAY
\    LDX #str_ptr
\    JSR get_for_stk     \  str_ptr => control var address
\    JSR load_W_SP_S     \  W => control var value
\    LDX #O_reg
\    JSR get_for_stk     \  O => step size
\    PHA                 \  save sign of step
\    JSR oper_ADD        \  Update the value
\    JSR store_W_SP_S    \  Store it back in control variable
\    JSR get_for_stk     \  O => limit
\    PLA
\    BMI _nxt_step_neg
\._nxt_step_poz          \  Go round again if current <= limit
\    JSR _tgt_tle
\    BPL _nxt_again
\    RTS
\._nxt_step_neg          \  Go round again if current >= limit
\    JSR _tlt_tge
\    BPL _nxt_again
\    RTS
\._nxt_again             \  Reset code_ptr to go round loop again
\    LDX #code_ptr
\    JSR get_for_stk     \  code_ptr => top of loop
\    RTS
\
\.get_for_stk
\    LDA (for_ptr), Y
\    STA 0, X
\    INY
\    LDA (for_ptr), Y
\    STA 1, X
\    INY
\    RTS
\
\\  MAKE A NEW ENTRY ON THE FOR STACK

\  CALC STACK ON ENTRY;   CONTROL VARIABLE LOCATION  <- IN W
\                         STEP SIZE
\                         FINISHING VALUE
\                         STARTING VALUE
\
\  FOR STACK AFTERWARDS;  CONTROL VARIABLE LOCATION
\                         STEP SIZE
\                         FINISHING VALUE
\                         CONTINUE ADDRESS

.oper_FOR
    LDX #var_ptr        \  On entry W = control variable location
    JSR copy_W_out
    JSR pull_W          \  Next is step size, pull into W
    LDX #X_reg          \  Next is finishing value, pull into H
    JSR pull16
    LDX #O_reg          \  Last is starting value, pull into O
    LDY #0              \  Now copy these values to FOR stack
._for_1
    LDA W_reg, Y        \  Unfortunately there is no short, Y form
    STA (for_ptr), Y
    INY
    CPY #6
    BNE _for_1          \  Keep going till we have copied six bytes
    LDA code_ptr        \  Bytes 6 and 7 on FOR stack are continue address
    STA (for_ptr), Y    \  (which we get from the code pointer)
    INY
    LDA code_ptr+1
    STA (for_ptr), Y
    INY
    LDX #for_ptr        \  Advance the FOR stack pointer 8 bytes
    JSR adv_ptr_X
    LDY #0              \  Initialise the control variable
    LDA O_reg           \  O = starting value
    STA (var_ptr), Y
    INY
    LDA O_reg+1
    STA (var_ptr), Y
    RTS

    
\  READ A WORD POINTED TO BY THE O REGISTER INTO THE W REGISTER

.core_GET
    LDY #0
    LDA (O_reg), Y
    STA W_reg
    INY
    LDA (O_reg), Y
    STA W_reg+1
    RTS

\  STORE THE W REGISTER AT THE ADDRESS POINTED TO BY THE O REGISTER

.core_PUT
    LDY #0
    LDA W_reg
    STA (O_reg), Y
    INY
    LDA W_reg+1
    STA (O_reg), Y
    RTS
    
\  READ A WORD POINTED TO BY THE VARIABLE POINTER INTO THE W REGISTER
\  "SAFE" VERSION PUSHES Y ON 6502 STACK

.load_W_VP_S
    TYA
    PHA
    JSR load_W_VP
    PLA
    TAY
    RTS
.load_W_VP
    LDY #0
    LDA (var_ptr), Y
    STA W_reg
    INY
    LDA (var_ptr), Y
    STA W_reg+1
    RTS
    

\  STORE THE W REGISTER AT THE ADDRESS POINTED TO BY THE VARIABLE POINTER
\  "SAFE" VERSION PUSHES Y ON 6502 STACK AND RESTORES AFTERWARDS

.store_W_VP_S
    TYA
    PHA
    JSR store_W_VP
    PLA
    TAY
    RTS
.store_W_VP
    LDY #0
    LDA W_reg
    STA (var_ptr), Y
    INY
    LDA W_reg+1
    STA (var_ptr), Y
    RTS

.get_instr
    LDY #0
    LDA (code_ptr), Y
    STA opcode          \  save this in its raw state
    BPL _gi_stk         \  skip if Stack mode
    CMP #&FF
    BNE _gi_not_done
._gi_done
    BRK
    EQUB 56
    EQUS "Program finished"
    BRK
._gi_not_done
    AND #&3F            \  mask out bit 6 in imm/ind modes
._gi_stk
    JSR inc_code_ptr    \  Now points either to next instruction,
                        \  or to operand for this instruction.
    JSR do_instr
._gi_next
    RTS                 \  One ? can make this into a JMP
    EQUW get_instr
.do_instr
    ASL A
    TAX
    LDA instr_table, X
    STA instr_addr
    LDA instr_table+1, X
    STA instr_addr+1
    TXA
    BNE _di_not_DUP_USE
    JSR push_W          \  duplicate the value on top of the stack
    BIT opcode          \  see if it's DUP or a USE variant
    BMI core_USE        \  USE works like a monadic
    RTS
._di_not_DUP_USE
    CPX #first_implicit-instr_table     \  first implicit operation
    BCS _di_not_dyadic
    JMP dyadic
._di_not_dyadic
    CPX #first_monadic-instr_table      \  first monadic operation
    BCS _di_not_implicit
    \JMP do_operation
    JMP (instr_addr)
._di_not_implicit
._di_monadic

\
\  STACK MODE MONADIC OPERATION
\  W is already top of stack.
\  Do operation, leaving result in W.
\
\  DIRECT MODE MONADIC OPERATION
\  Push stack
\  Load W from memory.
\  Do operation, leaving result in W / top of stack.
\
\  INDIRECT MODE MONADIC OPERATION
\  Push stack
\  Load W from memory.
\  Replace W with [W].
\  Do operation, leaving result in W / top of stack.

.monadic
    BIT opcode
    BPL _monad_stk      \  bit 7 = 0 => stack mode
    JSR push_W
.core_USE
    JSR copy_inst_W
    BIT opcode
    BVC _monad_dir      \  bit 6 = 1 => indirect
._monad_indir
    JSR indir_W         \  this won't touch V
._monad_stk
._monad_dir
    \JMP do_operation
    JMP (instr_addr)

.indir_W
    LDA (W_reg), Y
    PHA
    INY
    LDA (W_reg), Y
    STA W_reg+1
    PLA
    STA W_reg
    RTS

.push_W
    LDX #W_reg
    JMP newpush16

\  W IS ALWAYS TOP-OF-STACK!
\  STACK MODE DYADIC OPERATION
\
\  Put contents of W in O -- equivalent to pulling O from stack.
\  Pull W from stack.
\  Do operation, leaving result in W.
\
\  DIRECT MODE DYADIC OPERATION
\  Load O from memory.  (W is already top of stack!)
\  Do operation, leaving result in W / top of stack.
\
\  INDIRECT MODE DYADIC OPERATION
\  Load O from memory.
\  Replace O with [O].  (W is already top of stack!)
\  Do operation, leaving result in W / top of stack.

.dyadic
    BIT opcode
    BPL _dyad_stk       \  bit 7 = 0 => stack mode
    JSR copy_inst_O
    BIT opcode
    BVC _dyad_dir       \  bit 6 = 1 => indirect
._dyad_indir
    JSR indir_O         \  this won't touch V
    BVS _dyad_dir
._dyad_stk
    JSR copy_W_O        \  now O = old top of stack
    JSR pull_W          \  now W = old second on stack
._dyad_dir
    \JMP do_operation
    JMP (instr_addr)
    
.copy_inst_W
    LDX #W_reg
    BNE copy_inst
.copy_inst_O
    LDX #O_reg
.copy_inst
    LDY #0
    LDA (code_ptr), Y
    STA 0,X
    INY
    LDA (code_ptr), Y
    STA 1,X
    DEY
.inc_code_ptr2
    JSR inc_code_ptr
.inc_code_ptr
    INC code_ptr
    BNE _icp0
    INC code_ptr+1
._icp0
    RTS
    
.indir_O
    LDA (O_reg), Y
    PHA
    INY
    LDA (O_reg), Y
    STA O_reg+1
    PLA
    STA O_reg
    RTS

.copy_W_O
    LDX #O_reg
.copy_W_out
    LDA W_reg
    STA 0,X
    LDA W_reg+1
    STA 1,X
    RTS
        
.oper_USE
.oper_POW
.oper_CMP
.oper_BCP
.oper_ARG
.oper_ARP
.oper_RDW
.oper_RDB
    RTS

.oper_EOR
    LDA W_reg
    EOR O_reg
    STA W_reg
    LDA W_reg+1
    EOR O_reg+1
    STA W_reg+1
    RTS
    
.oper_ORI
    LDA W_reg
    ORA O_reg
    STA W_reg
    LDA W_reg+1
    ORA O_reg+1
    STA W_reg+1
    RTS

.oper_AND
    LDA W_reg
    AND O_reg
    STA W_reg
    LDA W_reg+1
    AND O_reg+1
    STA W_reg+1
    RTS

.oper_MOD
    JSR signed_divide   \  do the division
    
.copy_E_W
    LDX #X_reg
.copy_to_W
    LDA 0,X
    STA W_reg
    LDA 1,X
    STA W_reg+1
    RTS

\._copy_X_W              \  copy X register over W
\    LDA X_reg
\    STA W_reg
\    LDA X_reg+1
\    STA W_reg+1
\    RTS
    
.oper_MUF
    JSR mult16
    JMP copy_E_W
\    JMP _copy_X_W

.oper_BSU
    SEC
    LDA O_reg
    SBC W_reg
    STA W_reg
    LDA O_reg+1
    SBC W_reg+1
    STA W_reg+1
    RTS

\  DECODE A HEX NUMBER IN W AND STORE ITS ASCII REPRESENTATION AT THE
\  FAR END OF THE CALCULATION STACK.

.decode_hex_W
    LDA #16
    BNE decode_baseA_W
    
\  DECODE A DECIMAL NUMBER IN W AND STORE ITS ASCII REPRESENTATION AT THE
\  FAR END OF THE CALCULATION STACK.
    
.decode_dec_W
    LDA #10
.decode_baseA_W
    JSR set_divisor
    TAX                 \  we know A=0 here
._ddW1
    JSR real_div16
    \  Store the digit's ASCII code in scratch space
    LDA remainder
    CLC
    ADC #48
    CMP #58             \  see if we need to correct a hex digit
    BCC _ddW2
    ADC #6              \  here, C=1 so we add one fewer than we need
._ddW2
    STA calc_stk,X
    INX
    \  See if working total has reached zero
    LDA dividend
    ORA dividend+1
    BNE _ddW1
._ddW_end
    RTS

.oper_PRN
    JSR disp_dec_W
    JMP pull_W          \  replace what we just swallowed

.oper_PUT
    JSR core_PUT
    JMP pull_W

.oper_PAF
    JSR core_PAF
    JMP pull_W

\  STORE THE O REGISTER AT THE ADDRESS POINTED TO BY THE W REGISTER

.core_PAF
    LDY #0
    LDA O_reg
    STA (W_reg), Y
    INY
    LDA O_reg+1
    STA (W_reg), Y
    RTS

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\

\  Numeric comparisons work by subtracting O from W, or vice-versa, and
\  checking N and V.  (When a large positive number is subtracted from a
\  large negative number, a one being carried from bit 6 to bit 7 will
\  cause a false change of sign: the difference will have its highest
\  bit set to 0.  The V flag indicates such a condition has occurred.)
    
    
ALIGN &100

\  Eventually, this will be INCLUDEd from a separate file .....
\  .....  even with a tool to generate the correct instruction table
\  directly from the J-CODE opcodes.

.instr_table
    EQUW oper_USE       \ &00   DUP and USE are special
\  DYADIC OPERATIONS
    EQUW oper_EOR       \ &01
    EQUW oper_ORI       \ &02
    EQUW oper_AND       \ &03
    EQUW oper_ADD       \ &04
    EQUW oper_SUB       \ &05
    EQUW oper_MUL       \ &06
    EQUW oper_DIV       \ &07
    EQUW oper_MOD       \ &08
    EQUW oper_POW       \ &09
    EQUW oper_TEQ       \ &0A
    EQUW oper_TNE       \ &0B
    EQUW oper_TLT       \ &0C
    EQUW oper_TGE       \ &0D
    EQUW oper_TGT       \ &0E
    EQUW oper_TLE       \ &0F
    EQUW oper_CMP       \ &10
    EQUW oper_BCP       \ &11
    EQUW oper_BSU       \ &12
    EQUW oper_MUF       \ &13
    EQUW oper_DIP       \ &14
    EQUW oper_PUT       \ &15
    EQUW oper_PAF       \ &16
    EQUW oper_ARG       \ &17
    EQUW oper_ARP       \ &18
\  IMPLICIT OPERATIONS
.first_implicit
\  MONADIC OPERATIONS
.first_monadic
    EQUW oper_NCH       \ &19
    EQUW oper_TWC       \ &1A
    EQUW oper_ONC       \ &1B
    EQUW oper_SGN       \ &1C
    EQUW oper_ABS       \ &1D
    EQUW oper_RDW       \ &1E
    EQUW oper_RDB       \ &1F

._code_end

SAVE "M.CODE", calc_stk, _code_end, _safe_return

PRINT "init_calc_stk =", ~init_calc_stk
PRINT "_gi_next =", ~_gi_next
PRINT "_gi_done =", ~_gi_done
